buildscript {
  repositories { mavenCentral() }
}

plugins {
  id 'com.netflix.nebula.netflixoss' version '11.1.1'
  id 'org.checkerframework' version '0.6.35'
}

subprojects {
  apply plugin: 'nebula.netflixoss'

  java {
    toolchain {
      languageVersion = JavaLanguageVersion.of(8)
    }
  }

  apply plugin: 'org.checkerframework'
  checkerFramework {
    checkers = [
      'org.checkerframework.checker.optional.OptionalChecker'
    ]
    extraJavacArgs = [
      '-AsuppressWarnings=type.anno.before.modifier,type.anno.before.decl.anno',
      '-AassumePure',
      '-AwarnUnneededSuppressions'
    ]
    excludeTests = true
    ext.checkerFrameworkVersion = '3.41.1-SNAPSHOT'
    dependencies {
      compileOnly "org.checkerframework:checker-qual:${checkerFrameworkVersion}"
      testCompileOnly "org.checkerframework:checker-qual:${checkerFrameworkVersion}"
      checkerFramework "org.checkerframework:checker:${checkerFrameworkVersion}"
    }
    configurations.all {
      resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }
  }

  tasks.withType(Javadoc) {
    options.tags(
        'apiNote:a:API Note:',
        'implSpec:a:Implementation Requirements:',
        'implNote:a:Implementation Note:')
  }
  tasks.withType(Javadoc).all { enabled = false }

  group = 'com.netflix.hollow'

  repositories {
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
  }
}
